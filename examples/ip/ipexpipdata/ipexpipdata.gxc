//===========================================================================
NAME          = "Export to Geosoft IPDATA format File(s)"
#include <version.gxh>

TRANSLATIONDOMAIN = "ip"
TRANSLATIONFRIENDLYPATH = "GX/IP/Export"

DESCRIPTION   = "
   Export to Geosoft IPDATA format file(s) from the current database.

    IP.LINES : Line selections:
               A : All lines
               S : Selected lines
               D : Displayed line
    IP.IPDATATITLE : Title for IPDATA files
"
// Revision History:
// v5.1.3: Using "Current" X, Y and Z channels - WinnowChanList2_IP.
//===========================================================================
//                                 RESOURCES
//===========================================================================

RESOURCE = "IPEXPIPDATA.gr"
#include "IPEXPIPDATA.grh"

//===========================================================================
//                                  INCLUDE
//===========================================================================

#include <all.gxh>     // system

//===========================================================================
//                                 VARIABLES
//===========================================================================

EDB         EData;            // Database handle
DB          Data;             // Database handle
DGW         Diag;             // Dialogue handle
IP          Ip;               // IP handle
DB_SELECT   Select;           // current selection mode
LST         List;

int         i;                // Utility 

string(STR_DB_SYMBOL) sCurLine;
string(STR_DEFAULT_LONG) sTitle;
string(STR_DEFAULT_SHORT) sLines;
string(STR_DB_SYMBOL) sChan;
string(STR_DEFAULT) sDir;
int iDone;

//===========================================================================
//                                   CODE
//===========================================================================

{
   // --- get database ---
   
   EData = Current_EDB();
   
   // --- Get currently selected line ---

   GetCurLine_EDB(EData,sCurLine);

   Data = Lock_EDB(EData);


   // --- Are we running interactively ? ---

   iDone = 0;
   while (iInteractive_SYS() && !iDone)
   {
      // --- Create the Dialogue ---

      Diag = Create_DGW("IPEXPIPDATA");

      // --- Create VA channel list ---

      List = GetList_DGW(Diag,_IPEXPIPDATA_0);
      SymbLST_DB(Data,List,DB_SYMB_CHAN);
      WinnowChanList2_IP(List, Data);
      Sort_LST(List,0,0);

      // --- Set any Defaults ---

      SetInfoSYS_DGW(Diag,_IPEXPIPDATA_0,DGW_TEXT,"IP","IPCHAN");
      SetInfoSYS_DGW(Diag,_IPEXPIPDATA_1,DGW_LISTALIAS,"IP","LINES");
      SetInfoSYS_DGW(Diag,_IPEXPIPDATA_2,DGW_TEXT,"IP","IPDATATITLE");

      // --- Run the Dialogue ---

      i = iRunDialogue_DGW(Diag);
      if (i != 0) Cancel_SYS();    // He hit cancel


      // --- Get the Strings ---

      GetInfoSYS_DGW(Diag,_IPEXPIPDATA_0,DGW_TEXT,"IP","IPCHAN");
      GetInfoSYS_DGW(Diag,_IPEXPIPDATA_1,DGW_LISTALIAS,"IP","LINES");
      GetInfoSYS_DGW(Diag,_IPEXPIPDATA_2,DGW_TEXT,"IP","IPDATATITLE");


      // --- Destroy the Dialogue ---

      Destroy_DGW(Diag);

      // --- Verify input ---
 
      iDone = 1;
   } 
  
   // --- Get Parameters ---

   GetString_SYS("IP","LINES",sLines);
   ToUpper_STR(sLines);

   GetString_SYS("IP","IPDATATITLE",sTitle);
   GetString_SYS("IP","IPCHAN",sChan);

   // --- Create the IP object ---

   Ip = Create_IP();

   // --- turn on progress ---

   Progress_SYS(1);
   ProgName_SYS(_("Exporting data in IPDATA format"),1);


   // --- Do the line selection ---

   Select = GetSelect_DB(Data);
   if(iCharComp_STR(sLines,"D")) {
      Select_DB(Data,"",DB_LINE_SELECT_EXCLUDE);
      Select_DB(Data,sCurLine,DB_LINE_SELECT_INCLUDE);
   }
   else if(iCharComp_STR(sLines,"A")) {
      Select_DB(Data,"",DB_LINE_SELECT_INCLUDE);
   }

   // --- export ---
   if(iInteractive_SYS())
      ExportIPDATADir_IP(Ip, Data, sChan, sTitle, "");
   else {
      GetString_SYS("IPEXPIPDATA","DIR",sDir);
      ExportIPDATADir_IP(Ip, Data, sChan, sTitle, sDir);

   }


   // --- Restore the line selection ---

   SetSelect_DB(Data, Select);


   // --- release database to EDB ---
   
   UnLock_EDB(EData);
   
   
   // --- progress off ---

   Progress_SYS(0);

   Destroy_IP(Ip);
   
}
